
apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.8
version = '1.0'

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.4'
    compile 'org.rundeck:rundeck-core:3.4.0-20210614'
    pluginLibs 'com.google.api-client:google-api-client:1.33.0'
    pluginLibs 'com.google.apis:google-api-services-drive:v3-rev20211107-1.32.1'
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Name' : 'Google Drive Notification'
        attributes 'Rundeck-Plugin-Classnames': 'org.rundeck.plugin.GoogleDriveNotificationPlugin'
        attributes 'Rundeck-Plugin-File-Version': "${project.version}"
        attributes 'Rundeck-Plugin-Description' : 'Upload a file to google drive with Rundeck'
        attributes 'Rundeck-Plugin-Version': '1.2'
        attributes 'Rundeck-Plugin-Archive': 'true'
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
